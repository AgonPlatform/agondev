	.assume	adl=1

	.section	.text
	.global	__lxor

__lxor:
; CC: 28*r(PC)+11*r(SPL)+10*w(SPL)+1
	push	af
	xor	a, e
	ld	e, a
;	jr	__ixor.hijack_lxor
;	virtual
;		ld	a, 0
;		load .ld_a: byte from $$
;	end virtual
;	db	.ld_a	; push af -> ld a,*

    db 0x3E ; Only the first byte for opcode <LD A, *>
	.extern	__ixor

	.section	.text
	.global	__ixor

__ixor:
; CC: 24*r(PC)+11*r(SPL)+10*w(SPL)+1
	push	af
	.local	__ixor.hijack_lxor
.hijack_lxor:
	push	hl
	scf
	sbc	hl, hl
	add	hl, sp
	push	bc
	ld	a, (hl)
	inc	hl
	ld	sp, hl
	inc	hl
	inc	hl
	xor	a, (hl)
	ld	(hl), a
	pop	hl
	ld	a, h
	xor	a, b
	ld	h, a
	ld	a, l
	xor	a, c
	ld	l, a
	pop	af
	ret
